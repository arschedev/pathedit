#!/usr/bin/perl

##
# Copyright (c) 2023 arschedev
#

use strict;
use warnings;

my $VERSION  = "1.4.0-dev.3";
my $CODENAME = "undef";
my $DATE     = "2023-10-09";

#
# Set up
#

## Utilities
sub is;
sub isNot;
sub trim;
sub getShell;
sub println;
sub confirm;
sub wrapText;
sub readFile;
sub writeFile;
sub appendFile;
sub appendFileAt;
sub findLineAtFile;
sub touchFile;
sub getDir;
sub pathFmt;

## Configuration
my $OS          = $^O;
my $home        = glob "~";            # /home/arschedev
my $shell       = getShell;            # $SHELL
my $profilePath = "$home/.profile";    # .profile path
my $rcFile      = "";                  # shell init file name

## End messages
my %completeMessage = (
    "re_login"    => "",
    "update_path" => ""
);

## ...
my %rcFileTable = (
    "bash" => sub { $rcFile = ".bashrc" },
    "csh"  => sub { $rcFile = ".cshrc" },
    "dash" => sub { $rcFile = ".shinit" },
    "ksh"  => sub { $rcFile = ".shinit" },
    "sh"   => sub {
        $rcFile          = ".profile";
        %completeMessage = (
            "re_login"    => "Please re-login to apply all changes",
            "update_path" => "Run `source ~/$rcFile` to update PATH\nOr re-login to apply all changes"
        );
    },
    "tcsh"    => sub { $rcFile = ".tcshrc" },
    "zsh"     => sub { $rcFile = ".zshrc" },
    "default" => sub { print "Your shell is currently not supported: $shell\n" }
);

# ...
if ( exists $rcFileTable{$shell} ) {
    $rcFileTable{$shell}->();

    # default end message
    if ( isNot( $shell, ( "sh", "ksh", "dash" ) ) ) {
        %completeMessage = (
            "re_login"    => "Please re-login into your terminal to update PATH",
            "update_path" => "Run `source ~/$rcFile` to update PATH\nWithout logging out"
        );
    }

    # .shinit
    if ( $rcFile eq ".shinit" ) {    # dash, ksh
        touchFile "$home/.profile";

        # set up .shinit
        if ( readFile($profilePath) !~ /# PATHEDIT\nENV=\$HOME\/\.shinit; export ENV/m ) {
            appendFile( $profilePath, "\n\n# PATHEDIT\nENV=\$HOME/.shinit; export ENV" );    # set ENV
        }

        # specific end message
        %completeMessage = (
            "re_login"    => "Please re-login to apply all changes",
            "update_path" => "Run `source ~/$rcFile` to update PATH\nOr re-login to apply all changes"
        );
    }
} else {
    $rcFileTable{"default"}->();    # default case
    exit 1;
}

# Final shell init file path
my $rcFilePath = "$home/$rcFile";

#
# Main section
#

# If linux
my $if_darwin    = "";
my $notSupported = "";

# If mac
if ( $OS eq "darwin" ) {
    $notSupported = "(not supported)";
    $if_darwin    = " for mac";
}

## Help section
my $help = "Usage: pathedit [MODE | COMMAND] [PATH]

    Mode:
        +,  add\t\tadd to PATH
        -,  rm\t\tremove from PATH
        --, rmf\t\tforce remove from PATH $notSupported
        brm\t\tremove binary path from PATH $notSupported

    Command:
        -l, -list\tlist PATH
        -r, -reveal\treveal ~/$rcFile
        -P, -profile\treveal ~/.profile
        -v, -version\tprint version
        -h, -help\tprint this message

    Shortcuts:
        <mode> current\tadd/remove current directory from PATH

    Examples:
        pathedit add ~/bin/jdk/bin\tadd ~/bin/jdk/bin to PATH
        pathedit rm  ~/bin/jdk/bin\tremove ~/bin/jdk/bin from PATH
        pathedit rmf /usr/local/games\tforce remove /usr/local/games from PATH\n\t\t\t\t\t-> PATH=\${PATH/':/usr/local/games'/}
        pathedit brm java\t\tremove java path from PATH\n\t\t\t\t\t-> force remove ~/bin/jdk/bin
        pathedit add current\t\tadd current directory to PATH
        pathedit rm  current\t\tremove current directory from PATH
        pathedit rmf current\t\tforce remove current directory from PATH
        pathedit -list\t\t\tprint all directories listed in PATH
        pathedit -profile\t\topen ~/.profile on PATHEDIT line
        pathedit -reveal\t\topen ~/$rcFile on PATHEDIT line\n\t";

### CLI
if ( scalar @ARGV == 0 ) {    # default
    my $pathedit = wrapText( "arschedev's pathedit$if_darwin\n version $VERSION \"$CODENAME\"", "*" );
    println $pathedit . "\n" . $help;
    exit 1;
}

# Create shell init file or .shinit & .profile if not created
touchFile "$home/$rcFile";
touchFile "$home/.profile";

## Commands
my $commands = "(-l|-list|-r|-reveal|-P|-profile|-v|-version|-h|-help|-V|-S)";

# If a command
if ( scalar @ARGV > 0 and $ARGV[0] =~ /^$commands$/m ) {
    my $command = $ARGV[0];    #

    # list
    if ( is( $command, ( "-l", "-list" ) ) ) {
        println join( "\n", split( ":", `echo \$PATH` ) );
        exit 0;
    }

    # reveal & profile
    if ( is( $command, ( "-r", "-reveal", "-P", "-profile" ) ) ) {
        my $file = is( $command, ( "-r", "-reveal" ) ) ? $rcFilePath : $profilePath;
        my $line = findLineAtFile( $file, "# PATHEDIT" );

        if ( $OS eq "darwin" ) {
            println "Scroll to # PATHEDIT\n";
            system "open -e $file";    # open in TextEdit
        } else {
            my $editor = `gnome-text-editor -V 2>&1` ? "gnome-text-editor" : ( `gedit -V 2>&1` ? "gedit" : 0 );

            if ($editor) {
                system "$editor $file +$line";
            } else {
                println "Scroll to # PATHEDIT\n";
                system "xdg-open $file";    # open in text editor by default
            }
        }

        exit 0;
    }

    # version
    if ( is( $command, ( "-v", "-version" ) ) ) {
        my $perl = trim `perl -v | grep version`;    # perl quick info
        println "pathedit version \"$VERSION\" ($CODENAME) $DATE\n$perl\nhttps://github.com/arschedev/pathedit";
        exit 0;
    }

    # help
    if ( $command =~ /(-h|-help)/ ) {                # don't touch
        my $pathedit = wrapText( "arschedev's pathedit$if_darwin\n version $VERSION \"$CODENAME\"", "*" );
        println $pathedit . "\n" . $help;
        exit 0;
    }

    # -V
    if ( is( $command, ("-V") ) ) {
        println "$VERSION";
        exit 0;
    }

    # -S
    if ( is( $command, ("-S") ) ) {
        println $rcFile;
        exit 0;
    }
}

# Then if not a command
if ( scalar @ARGV < 2 ) {
    println wrapText( "Error: no such command\n Error: too few arguments", "=" ) . "\n" . $help;
    exit 1;
}

## Modes
my $mode = $ARGV[0];            #
my $path = pathFmt $ARGV[1];    # remove rest /

# If not a mode
if ( isNot( $mode, ( "+", "add", "-", "rm", "--", "rmf", "brm" ) ) ) {
    println wrapText( "Error: no such mode...\n Error: `$mode` not found", "=" ) . "\n" . $help;
    exit 1;
}

# :
if ( $path =~ m/\:/ ) {
    println ": is not allowed";
    exit 2;
}

# Read shell init file
my $rcFileData = readFile $rcFilePath;

# PATHEDIT
if ( $rcFileData !~ /^# PATHEDIT$/m ) {    # don't touch
    appendFile( $rcFilePath, "\n\n# PATHEDIT" );
    $rcFileData = readFile $rcFilePath;    # update $rcFileData
}

# export
if ( $rcFileData !~ /^export PATH #$/m ) {    # don't touch
    appendFile( $rcFilePath, "\nexport PATH #" );
    $rcFileData = readFile $rcFilePath;
}

# Shortcuts
if ( is( $mode, ( "+", "add", "-", "rm", "--", "rmf" ) ) ) {
    if ( $path eq "current" ) {
        $path = trim `pwd`;    # present working directory
        println $path . "\n";
    }
}

# add
if ( is( $mode, ( "+", "add" ) ) ) {

    # removing trace of rmf
    if ( $rcFileData =~ /\nPATH=\$\{PATH\/\':\Q${path}\E\/?\'\/\} #.+$/gm ) {
        $rcFileData =~ s/\nPATH=\$\{PATH\/\':\Q${path}\E\/?\'\/\} #.+$//gm;
        writeFile( $rcFilePath, $rcFileData );

        println "OK (add)";
        println $completeMessage{"re_login"};
        exit 0;
    }

    my $export = "PATH=\$PATH:$path # add $path\n";

    appendFileAt( $rcFilePath, $export, findLineAtFile( $rcFilePath, "export PATH #" ) );    # append export line

    println "OK (add)";
    println $completeMessage{"update_path"};
    exit 0;
}

# remove
if ( is( $mode, ( "-", "rm" ) ) ) {
    if ( $rcFileData !~ /\nPATH=\$PATH:\Q${path}\E\/? #.+$/gm ) {
        println "Not Found (rm)";
        exit 2;
    }

    $rcFileData =~ s/\nPATH=\$PATH:\Q${path}\E\/? #.+$//gm;    # remove requested export line

    writeFile( $rcFilePath, $rcFileData );                     # apply changes

    println "OK (rm)";
    println $completeMessage{"re_login"};
    exit 0;
}

# force remove
if ( is( $mode, ( "--", "rmf" ) ) ) {
    if ( $OS eq "darwin" ) {
        println "Not supported (rmf on macOS)\n";
        exit 1;
    }

    my $export = "PATH=\${PATH/':$path'/} # rmf $path\n";

    appendFileAt( $rcFilePath, $export, findLineAtFile( $rcFilePath, "export PATH #" ) );    # append export line

    println "OK (rmf)";
    println $completeMessage{"re_login"};
    exit 0;
}

# binary path remove
if ( is( $mode, ("brm") ) ) {
    if ( $OS eq "darwin" ) {
        println "Not supported (brm on macOS)\n";
        exit 1;
    }

    my $dir = getDir $path;    # get binary path

    if ( $dir eq "" ) {
        println "Not Found (brm)";
        exit 2;
    }

    # User confirmation
    println "Path to be removed from PATH: $dir";
    my $res = confirm();

    # Removal
    if ( $res == 1 ) {
        if ( $rcFileData =~ /\nPATH=\$PATH:\Q${dir}\E\/? #.+$/gm ) {
            $rcFileData =~ s/\nPATH=\$PATH:\Q${dir}\E\/? #.+$//gm;    # rm
            writeFile( $rcFilePath, $rcFileData );                    # apply changes
        } else {
            my $export = "PATH=\${PATH/':$dir'/} # brm $path\n";                                    # rmf
            appendFileAt( $rcFilePath, $export, findLineAtFile( $rcFilePath, "export PATH #" ) );   # append export line
        }

        println "OK (brm)";
        println $completeMessage{"re_login"};
        exit 0;
    }

    println "Canceled (brm)";
}

#
# Utilities
#

sub is {
    my ( $var, @values ) = @_;

    foreach my $value (@values) {
        if ( $var eq $value ) {
            return 1;
        }
    }

    return 0;
}

#
sub isNot {
    my ( $var, @values ) = @_;
    return !is( $var, @values );
}

#
sub trim {
    my $string = $_[0];
    $string =~ s/^\s+|\s+$//g;    # remove whitespaces from both sides
    return $string;
}

#
sub getShell {
    my @path = split( "/", `echo \$SHELL` );
    return trim $path[-1];        # return last token / shell name
}

#
sub println {
    print "$_[0]\n";
}

#
sub confirm {
    while (1) {
        print "OK? (y/n) ";
        my $res = <STDIN>;

        return 1 if ( $res =~ /^[yY]$/m );
        return 0 if ( $res =~ /^[nN]$/m );
    }
}

#
sub wrapText {
    my $text    = $_[0];
    my $wrapper = $_[1];

    my $len  = length($text) + 2;    # text length + 2 whitespaces on the sides
    my $wrap = $wrapper x $len;      # repeat wrapper len times

    return "$wrap\n $text\n$wrap\n";
}

#
sub readFile {
    my $path = $_[0];

    open( my $FH, "<:encoding(UTF-8)", $path ) or die "Cannot open $path: $!";
    my $file = "";

    while (<$FH>) {                  # iterate through each line
        $file = $file . $_;          # accumulate lines
    }

    close $FH;
    return $file;
}

#
sub writeFile {
    my $path = $_[0];
    my $data = $_[1];

    open( my $fh, ">", $path ) or die "Cannot open $path: $!";
    print $fh $data;
    close $fh;
}

#
sub appendFile {
    my $path = $_[0];
    my $data = $_[1];

    open( my $fh, ">>", $path ) or die "Cannot open $path: $!";
    print $fh $data;
    close $fh;
}

#
sub appendFileAt {
    my $path = $_[0];
    my $data = $_[1];
    my $line = $_[2];

    open( my $fh, "<", $path ) or die "Cannot open $path: $!";
    my @lines = <$fh>;
    close $fh;

    splice( @lines, $line - 1, 0, $data );

    open( $fh, ">", $path ) or die "Cannot open $path: $!";
    print $fh @lines;
    close $fh;
}

#
sub findLineAtFile {
    my $path  = $_[0];
    my $query = $_[1];

    my $file  = readFile $path;
    my @lines = split( "\n", $file );
    my $line  = 0;

    for my $i ( 0 .. $#lines ) {
        if ( is( $lines[$i], ($query) ) ) {
            $line = $i + 1;
            last;
        }
    }

    return $line;
}

#
sub touchFile {
    my $file = $_[0];

    unless ( -e $file ) {
        writeFile( $file, "\n" );
    }
}

#
sub getDir {
    my $bin = $_[0];

    my @path = split( "/", `which $bin` );
    pop @path;    # remove binary name itself

    return join( "/", @path );
}

#
sub pathFmt {
    my @path = split( "", $_[0] );

    if ( $path[-1] eq "/" ) {    # if last token is /
        pop @path;               # remove it
    }

    return join( "", @path );
}

# TODO: Move exports to the end
# TODO: Refine error handling
# TODO: Complete macOS support
