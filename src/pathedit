#!/usr/bin/perl
use strict;
use warnings;

my $VERSION = "1.2.0-dev.6";
my $DATE    = "2023-05-02";

# trim
sub trim {
    my $string = $_[0];
    $string =~ s/^\s+|\s+$//g;
    return $string;
}

# shell
sub getShell {
    my @path = split( "/", `echo \$SHELL` );
    return trim( $path[-1] );
}

# Paths
my $OS      = $^O;
my $home    = glob "~";
my $shell   = getShell;
my $shellf  = ".$shell" . "rc";
my $bashrc  = "$home/$shellf";
my $profile = "$home/.profile";

# Utils
sub println {
    print "$_[0]\n";
}

#
sub promptYN {
    while (1) {
        print "OK? (y/n) ";

        my $res = <STDIN>;

        if ( $res =~ /^[yY]$/m ) {
            return 1;
        }

        if ( $res =~ /^[nN]$/m ) {
            return 0;
        }
    }
}

#
sub wrapText {
    my $text    = $_[0];
    my $wrapper = $_[1];

    my $len  = length($text) + 2;
    my $wrap = $wrapper x $len;

    return "$wrap\n $text\n$wrap\n";
}

#
sub readFile {
    my $path = $_[0];

    open( my $FH, "<:encoding(UTF-8)", $path );
    my $file = "";

    while (<$FH>) {
        $file = $file . $_;
    }

    close $FH;
    return $file;
}

#
sub writeFile {
    my $path = $_[0];
    my $data = $_[1];

    open( my $fh, ">", $path );
    print $fh $data;
    close $fh;
}

#
sub appendFile {
    my $path = $_[0];
    my $data = $_[1];

    open( my $fh, ">>", $path );
    print $fh $data;
    close $fh;
}

#
sub getDir {
    my $bin = $_[0];

    my @path = split( "/", `which $bin` );
    pop @path;

    return join( "/", @path );
}

#
sub pathFmt {
    my @path = split( "", $_[0] );
    if ( $path[-1] eq "/" ) {
        pop @path;
    }

    return join( "", @path );
}

#
sub sourceBashrcMsg {
    println "Run `source ~/$shellf` to update PATH\nWithout logging out";
}

#
sub reloginMsg {
    println "Please re-login into your terminal to update PATH";
}

# Main
my $ifdarwin     = "";
my $notsupported = "";

if ( $OS eq "darwin" ) {
    $notsupported = "(not supported)";
    $ifdarwin     = " for mac";
}

my $tabs = "\t" x 5;
my $help = "Usage: pathedit <mode|command> <path>

    Mode:
        +,  add\t\tadd to PATH
        -,  rm\t\tremove from PATH
        --, rmf\t\tforce remove from PATH $notsupported
        brm\t\tremove binary path from PATH $notsupported

    Command:
        -l, -list\tlist PATH
        -r, -reveal\treveal ~/$shellf
        -P, -profile\treveal ~/.profile
        -v, -version\tprint version
        -h, -help\tprint this message

    Shortcuts:
        <mode> current\tadd/remove current directory from PATH

    Examples:
        pathedit add ~/bin/jdk/bin\tadd ~/bin/jdk/bin to PATH
        pathedit rm  ~/bin/jdk/bin\tremove ~/bin/jdk/bin from PATH
        pathedit rmf /usr/local/games\tforce remove /usr/local/games from PATH\n$tabs-> PATH=\${PATH/':/usr/local/games'/}
        pathedit brm java\t\tremove java path from PATH\n$tabs-> force remove ~/bin/jdk/bin
        pathedit add current\t\tadd current directory to PATH
        pathedit rm  current\t\tremove current directory from PATH
        pathedit rmf current\t\tforce remove current directory from PATH
        pathedit -list\t\t\tprint all directories listed in PATH
        pathedit -profile\t\topen ~/.profile on PATHEDIT line
        pathedit -reveal\t\topen ~/$shellf on PATHEDIT line\n";

#
if ( scalar @ARGV == 0 ) {
    my $pathedit = wrapText( "arschedev's pathedit$ifdarwin\n version $VERSION", "*" );
    println $pathedit . "\n" . $help;
    exit 1;
}

# create .*rc & .profile if not created
system "touch ~/$shellf";
system "touch ~/.profile";

my $commands = "(-l|-list|-h|-help|-r|-reveal|-P|-profile|-v|-version)";
if ( scalar @ARGV > 0 and $ARGV[0] =~ /$commands/ ) {

    # list
    if ( $ARGV[0] =~ /^(-l|-list)$/m ) {
        println join( "\n", split( ":", `echo \$PATH` ) );
        exit 0;
    }

    # reveal
    if ( $ARGV[0] =~ /^(-r|-reveal|-P|-profile)$/m ) {
        my $bashrcf = readFile( $ARGV[0] =~ /^(-r|-reveal)$/m ? $bashrc : $profile );
        my $file    = $ARGV[0] =~ /^(-r|-reveal)$/m ? $bashrc : $profile;
        my @lines   = split( "\n", $bashrcf );
        my $line    = 0;

        for my $i ( 0 .. $#lines ) {
            if ( $lines[$i] =~ /^# PATHEDIT$/m ) {
                $line = $i + 1;
                last;
            }
        }

        if ( $OS eq "darwin" ) {
            println "Scroll to # PATHEDIT\n";
            system "open -e $file";
        }
        else {
            my $ifgedit = `gedit -V 2>&1` ? 1 : 0;

            if ($ifgedit) {
                system "gedit $file +$line";
            }
            else {
                println "Scroll to # PATHEDIT\n";
                system "xdg-open $file";
            }
        }

        exit 0;
    }

    # version
    if ( $ARGV[0] =~ /^(-v|-version)$/m ) {
        println
          "pathedit version \"$VERSION\" $DATE\nhttps://github.com/arschedev/pathedit\nhttps://github.com/arschedev";
        exit 0;
    }

    # help
    if ( $ARGV[0] =~ /(-h|-help)/ ) {
        my $pathedit = wrapText( "arschedev's pathedit$ifdarwin\n version $VERSION", "*" );
        println $pathedit . "\n" . $help;
        exit 0;
    }
}

if ( scalar @ARGV < 2 ) {
    println wrapText( "Error: no such command\n Error: too few arguments", "=" ) . "\n" . $help;
    exit 1;
}

my $mode = $ARGV[0];
my $path = pathFmt $ARGV[1];

# +|add|-|rm|--|rmf
if ( $mode !~ /^(\+|add|-|rm|--|rmf|brm)$/m ) {
    println wrapText( "Error: no such mode...\n Error: `$mode` not found", "=" ) . "\n" . $help;
    exit 1;
}

# bashrc file
my $bashrcf  = readFile $bashrc;
my $profilef = readFile $profile;

# PATHEDIT
if ( $bashrcf !~ /^# PATHEDIT$/m ) {
    appendFile( $bashrc, "\n\n# PATHEDIT" );
    $bashrcf = readFile $bashrc;
}

if ( $profilef !~ /^# PATHEDIT$/m ) {
    appendFile( $profile, "\n\n# PATHEDIT" );
    $profilef = readFile $profile;
}

# shortcuts
if ( $mode =~ /^(\+|add|-|rm|--|rmf)$/ ) {
    if ( $path eq "current" ) {
        $path = trim(`pwd`);
        println $path . "\n";
    }
}

# add
if ( $mode =~ /^(\+|add)$/m ) {
    my $export = "\nPATH=\$PATH:$path # add $path";
    appendFile( $bashrc,  $export );
    appendFile( $profile, $export );
    println "OK (+)";
    sourceBashrcMsg;
    exit 0;
}

# remove
if ( $mode =~ /^(-|rm)$/m ) {
    if ( $bashrcf !~ /\nPATH=\$PATH:\Q${path}\E\/? #.+$/gm and $profilef !~ /\nPATH=\$PATH:\Q${path}\E\/? #.+$/gm ) {
        println "Not Found (-)";
        exit 2;
    }

    $bashrcf  =~ s/\nPATH=\$PATH:\Q${path}\E\/? #.+$//gm;
    $profilef =~ s/\nPATH=\$PATH:\Q${path}\E\/? #.+$//gm;
    writeFile( $bashrc,  $bashrcf );
    writeFile( $profile, $profilef );
    println "OK (-)";
    reloginMsg;
    exit 0;
}

# force remove
if ( $mode =~ /^(--|rmf)$/m ) {
    if ( $OS eq "darwin" ) {
        println "Not supported (rmf at macOS)\n";
        exit 1;
    }

    my $export = "\nPATH=\${PATH/':$path'/} # rmf $path";
    appendFile( $bashrc,  $export );
    appendFile( $profile, $export );
    println "OK (--)";
    reloginMsg;
    exit 0;
}

# binary path remove
if ( $mode =~ /^brm$/m ) {
    if ( $OS eq "darwin" ) {
        println "Not supported (brm at macOS)\n";
        exit 1;
    }

    my $dir = getDir($path);

    if ( $dir eq "" ) {
        println "Not Found (brm)";
        exit 2;
    }

    println "Path to be removed from PATH: " . $dir;

    my $res = promptYN;

    if ( $res == 1 ) {
        my $export = "\nPATH=\${PATH/':$dir'/} # brm $dir";
        appendFile( $bashrc,  $export );
        appendFile( $profile, $export );
        println "OK (brm)";
        reloginMsg;
        exit 0;
    }

    println "Canceled (brm)";
}

# TODO: Refactor
# TODO: Refine error messages
# TODO: Full macOS support
