#!/usr/bin/perl
use strict;
use warnings;

my $VERSION = "1.3.0-dev.0";
my $DATE    = "2023-05-16";

sub trim;
sub getShell;

# Paths
my $OS      = $^O;
my $home    = glob "~";
my $shell   = getShell;
my $xrcfn   = ".$shell" . "rc";
my $xrc     = "$home/$xrcfn";
my $profile = "$home/.profile";

# Utils
sub println;
sub confirm;
sub wrapText;
sub readFile;
sub writeFile;
sub appendFile;
sub touchFile;
sub getDir;
sub pathFmt;

#
my %message = (
    "re_login"    => "Please re-login into your terminal to update PATH",
    "update_path" => "Run `source ~/$xrcfn` to update PATH\nWithout logging out"
);

###
my $ifdarwin     = "";
my $notsupported = "";

if ( $OS eq "darwin" ) {
    $notsupported = "(not supported)";
    $ifdarwin     = " for mac";
}

my $tabs = "\t" x 5;
my $help = "Usage: pathedit <mode|command> <path>

    Mode:
        +,  add\t\tadd to PATH
        -,  rm\t\tremove from PATH
        --, rmf\t\tforce remove from PATH $notsupported
        brm\t\tremove binary path from PATH $notsupported

    Command:
        -l, -list\tlist PATH
        -r, -reveal\treveal ~/$xrcfn
        -P, -profile\treveal ~/.profile
        -v, -version\tprint version
        -h, -help\tprint this message

    Shortcuts:
        <mode> current\tadd/remove current directory from PATH

    Examples:
        pathedit add ~/bin/jdk/bin\tadd ~/bin/jdk/bin to PATH
        pathedit rm  ~/bin/jdk/bin\tremove ~/bin/jdk/bin from PATH
        pathedit rmf /usr/local/games\tforce remove /usr/local/games from PATH\n$tabs-> PATH=\${PATH/':/usr/local/games'/}
        pathedit brm java\t\tremove java path from PATH\n$tabs-> force remove ~/bin/jdk/bin
        pathedit add current\t\tadd current directory to PATH
        pathedit rm  current\t\tremove current directory from PATH
        pathedit rmf current\t\tforce remove current directory from PATH
        pathedit -list\t\t\tprint all directories listed in PATH
        pathedit -profile\t\topen ~/.profile on PATHEDIT line
        pathedit -reveal\t\topen ~/$xrcfn on PATHEDIT line\n";

#
if ( scalar @ARGV == 0 ) {
    my $pathedit = wrapText( "arschedev's pathedit$ifdarwin\n version $VERSION", "*" );
    println $pathedit . "\n" . $help;
    exit 1;
}

# create .*rc & .profile if not created
touchFile "~/$xrcfn";
touchFile "~/.profile";

my $commands = "(-l|-list|-h|-help|-r|-reveal|-P|-profile|-v|-version)";
if ( scalar @ARGV > 0 and $ARGV[0] =~ /$commands/ ) {
    my $command = $ARGV[0];

    # list
    if ( $command =~ /^(-l|-list)$/m ) {
        println join( "\n", split( ":", `echo \$PATH` ) );
        exit 0;
    }

    # reveal
    if ( $command =~ /^(-r|-reveal|-P|-profile)$/m ) {
        my $xrcf  = readFile( $command =~ /^(-r|-reveal)$/m ? $xrc : $profile );
        my $file  = $command =~ /^(-r|-reveal)$/m ? $xrc : $profile;
        my @lines = split( "\n", $xrcf );
        my $line  = 0;

        for my $i ( 0 .. $#lines ) {
            if ( $lines[$i] =~ /^# PATHEDIT$/m ) {
                $line = $i + 1;
                last;
            }
        }

        if ( $OS eq "darwin" ) {
            println "Scroll to # PATHEDIT\n";
            system "open -e $file";
        }
        else {
            my $ifgedit = `gedit -V 2>&1` ? 1 : 0;

            if ($ifgedit) {
                system "gedit $file +$line";
            }
            else {
                println "Scroll to # PATHEDIT\n";
                system "xdg-open $file";
            }
        }

        exit 0;
    }

    # version
    if ( $command =~ /^(-v|-version)$/m ) {
        println
          "pathedit version \"$VERSION\" $DATE\nhttps://github.com/arschedev/pathedit\nhttps://github.com/arschedev";
        exit 0;
    }

    # help
    if ( $command =~ /(-h|-help)/ ) {
        my $pathedit = wrapText( "arschedev's pathedit$ifdarwin\n version $VERSION", "*" );
        println $pathedit . "\n" . $help;
        exit 0;
    }
}

if ( scalar @ARGV < 2 ) {
    println wrapText( "Error: no such command\n Error: too few arguments", "=" ) . "\n" . $help;
    exit 1;
}

my $mode = $ARGV[0];
my $path = pathFmt $ARGV[1];

# +|add|-|rm|--|rmf
if ( $mode !~ /^(\+|add|-|rm|--|rmf|brm)$/m ) {
    println wrapText( "Error: no such mode...\n Error: `$mode` not found", "=" ) . "\n" . $help;
    exit 1;
}

# bashrc file
my $xrcf     = readFile $xrc;
my $profilef = readFile $profile;

# PATHEDIT
if ( $xrcf !~ /^# PATHEDIT$/m ) {
    appendFile( $xrc, "\n\n# PATHEDIT" );
    $xrcf = readFile $xrc;
}

if ( $profilef !~ /^# PATHEDIT$/m ) {
    appendFile( $profile, "\n\n# PATHEDIT" );
    $profilef = readFile $profile;
}

# shortcuts
if ( $mode =~ /^(\+|add|-|rm|--|rmf)$/ ) {
    if ( $path eq "current" ) {
        $path = trim `pwd`;
        println $path . "\n";
    }
}

# add
if ( $mode =~ /^(\+|add)$/m ) {
    my $export = "\nPATH=\$PATH:$path # add $path";

    appendFile( $xrc,     $export );
    appendFile( $profile, $export );

    println "OK (+)";
    println $message{"update_path"};
    exit 0;
}

# remove
if ( $mode =~ /^(-|rm)$/m ) {
    if ( $xrcf !~ /\nPATH=\$PATH:\Q${path}\E\/? #.+$/gm && $profilef !~ /\nPATH=\$PATH:\Q${path}\E\/? #.+$/gm ) {
        println "Not Found (-)";
        exit 2;
    }

    $xrcf     =~ s/\nPATH=\$PATH:\Q${path}\E\/? #.+$//gm;
    $profilef =~ s/\nPATH=\$PATH:\Q${path}\E\/? #.+$//gm;

    writeFile( $xrc,     $xrcf );
    writeFile( $profile, $profilef );

    println "OK (-)";
    println $message{"re_login"};
    exit 0;
}

# force remove
if ( $mode =~ /^(--|rmf)$/m ) {
    if ( $OS eq "darwin" ) {
        println "Not supported (rmf at macOS)\n";
        exit 1;
    }

    my $export = "\nPATH=\${PATH/':$path'/} # rmf $path";

    appendFile( $xrc,     $export );
    appendFile( $profile, $export );

    println "OK (--)";
    println $message{"re_login"};
    exit 0;
}

# binary path remove
if ( $mode =~ /^brm$/m ) {
    if ( $OS eq "darwin" ) {
        println "Not supported (brm at macOS)\n";
        exit 1;
    }

    my $dir = getDir $path;

    if ( $dir eq "" ) {
        println "Not Found (brm)";
        exit 2;
    }

    println "Path to be removed from PATH: $dir";
    my $res = confirm();

    if ( $res == 1 ) {
        my $export = "\nPATH=\${PATH/':$dir'/} # brm $path";

        appendFile( $xrc,     $export );
        appendFile( $profile, $export );

        println "OK (brm)";
        println $message{"re_login"};
        exit 0;
    }

    println "Canceled (brm)";
}

## Utils
sub trim {
    my $string = $_[0];
    $string =~ s/^\s+|\s+$//g;
    return $string;
}

#
sub getShell {
    my @path = split( "/", `echo \$SHELL` );
    return trim $path[-1];
}

#
sub println {
    print "$_[0]\n";
}

#
sub confirm {
    while (1) {
        print "OK? (y/n) ";
        my $res = <STDIN>;

        return 1 if ( $res =~ /^[yY]$/m );
        return 0 if ( $res =~ /^[nN]$/m );
    }
}

#
sub wrapText {
    my $text    = $_[0];
    my $wrapper = $_[1];

    my $len  = length($text) + 2;
    my $wrap = $wrapper x $len;

    return "$wrap\n $text\n$wrap\n";
}

#
sub readFile {
    my $path = $_[0];

    open( my $FH, "<:encoding(UTF-8)", $path );
    my $file = "";

    while (<$FH>) {
        $file = $file . $_;
    }

    close $FH;
    return $file;
}

#
sub writeFile {
    my $path = $_[0];
    my $data = $_[1];

    open( my $fh, ">", $path );
    print $fh $data;
    close $fh;
}

#
sub appendFile {
    my $path = $_[0];
    my $data = $_[1];

    open( my $fh, ">>", $path );
    print $fh $data;
    close $fh;
}

#
sub touchFile {
    system "touch $_[0]";
}

#
sub getDir {
    my $bin = $_[0];

    my @path = split( "/", `which $bin` );
    pop @path;

    return join( "/", @path );
}

#
sub pathFmt {
    my @path = split( "", $_[0] );
    if ( $path[-1] eq "/" ) {
        pop @path;
    }

    return join( "", @path );
}

# TODO: Refine error handling
# TODO: Improve shell support
# TODO: Complete macOS support
